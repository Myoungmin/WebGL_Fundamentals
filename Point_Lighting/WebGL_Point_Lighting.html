<!doctype html>
<html>

<head>
  <title>WebGL_Fundamentals</title>
  <meta charset="utf-8">
  <link rel="stylesheet" type="text/css" href="./WebGL_Point_Lighting.css">
</head>

<body>

  <!-- 목차 공통으로 관리하기 위해 list.html 삽입 -->
  <!-- 출처 : https://kyung-a.tistory.com/18 -->
  <div data-include-path="../list.html"></div>
  <script>
    window.addEventListener('load', function () {
      var allElements = document.getElementsByTagName('*');
      Array.prototype.forEach.call(allElements, function (el) {
        var includePath = el.dataset.includePath;
        if (includePath) {
          var xhttp = new XMLHttpRequest();
          xhttp.onreadystatechange = function () {
            if (this.readyState == 4 && this.status == 200) {
              el.outerHTML = this.responseText;
            }
          };
          xhttp.open('GET', includePath, true);
          xhttp.send();
        }
      });
    });
  </script>

  <h2>WebGL Lighting : Point Lighting</h2>
  <p><a href="https://myoung-min.tistory.com/170" target="_blank">학습내용 블로그 정리</a></p>
  <canvas id="canvas"></canvas>
<div id="uiContainer">
  <div id="ui">
    <!--div id="fRotation"></div-->
    <h4>스칼라곱을 거듭제곱한 값<br>작을수록 밝아진다.</h4>
    <div id="shininess"></div>
  </div>
</div>
<!-- vertex shader -->
<script  id="vertex-shader-3d" type="x-shader/x-vertex">
attribute vec4 a_position;
attribute vec3 a_normal;

// 조명의 위치
uniform vec3 u_lightWorldPosition;
uniform vec3 u_viewWorldPosition;

uniform mat4 u_world;
uniform mat4 u_worldViewProjection;
uniform mat4 u_worldInverseTranspose;

varying vec3 v_normal;

varying vec3 v_surfaceToLight;
varying vec3 v_surfaceToView;

void main() {
  // 위치에 행렬 곱하기
  gl_Position = u_worldViewProjection * a_position;

  // 법선의 방향을 정하고 프래그먼트 셰이더로 전달
  v_normal = mat3(u_worldInverseTranspose) * a_normal;

  // 표면의 월드 위치를 계산하는 방법 : 위치에 월드 행렬을 곱한다.
  vec3 surfaceWorldPosition = (u_world * a_position).xyz;

  // 표면의 모든 위치에서 조명까지의 벡터를 계산한다.
  // fragment shader로 넘겨준다.
  v_surfaceToLight = u_lightWorldPosition - surfaceWorldPosition;

  // 표면 -> 뷰/카메라 벡터를 계산하고 프래그먼트 셰이더로 전달
  v_surfaceToView = u_viewWorldPosition - surfaceWorldPosition;
}
</script>
<!-- fragment shader -->
<script  id="fragment-shader-3d" type="x-shader/x-fragment">
precision mediump float;

// 정점 셰이더에서 전달됩니다.
varying vec3 v_normal;
varying vec3 v_surfaceToLight;
varying vec3 v_surfaceToView;

uniform vec4 u_color;
uniform float u_shininess;

void main() {
  // v_normal은 베링이기 때문에 보간되므로 단위 벡터가 아닙니다.
  // 정규화하면 다시 단위 벡터가 됩니다.
  vec3 normal = normalize(v_normal);

  vec3 surfaceToLightDirection = normalize(v_surfaceToLight);
  vec3 surfaceToViewDirection = normalize(v_surfaceToView);
  vec3 halfVector = normalize(surfaceToLightDirection + surfaceToViewDirection);

  float light = dot(normal, surfaceToLightDirection);
  float specular = 0.0;
  // 스칼라곱을 거듭제곱해서 밝기를 수정한다.
  if (light > 0.0) {
    // 음수를 거듭제곱하는 건 WebGL에 정의되어 있지 않아 스칼라곱이 음수가 된다면 specular를 0.0으로 남겨둔다.
    specular = pow(dot(normal, halfVector), u_shininess);
  }

  gl_FragColor = u_color;

  // 색상 부분(알파 제외)에만 광량 곱하기
  gl_FragColor.rgb *= light;

  // 반사율 더하기
  gl_FragColor.rgb += specular;
}
</script>

  <script src="../webgl-utils.js"></script>
  <script src="../webgl-lessons-ui.js"></script>
  <script src="../m4.js"></script>
  <script src="./WebGL_Point_Lighting.js"></script>
</body>

</html>